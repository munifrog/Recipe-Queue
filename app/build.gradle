apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

// https://stackoverflow.com/q/35722904
def keysFile = rootProject.file("my.keys")
def keysProperties = new Properties()
keysProperties.load(new FileInputStream(keysFile))

android {
    compileSdkVersion 28
    signingConfigs {
        release {
            storeFile file(keysProperties['release_trust_store_file'])
            storePassword keysProperties['release_trust_store_secret']
            keyPassword keysProperties['release_trust_key_secret']
            keyAlias keysProperties['release_trust_key_alias']
        }
    }
    defaultConfig {
        applicationId "com.example.recipe_q"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'API_KEY_SPOONACULAR', '\"' + keysProperties['api_spoonacular'] + '\"'
        // https://stackoverflow.com/a/28954193
        resValue 'string', 'ADMOB_KEY_INTERSTITIAL_APP_ID', '\"' + keysProperties['ad_mobs_application_release'] + '\"'
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'ADMOB_KEY_INTERSTITIAL_CODE', '\"' + keysProperties['ad_mobs_interstitial_testing'] + '\"'
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'ADMOB_KEY_INTERSTITIAL_CODE', '\"' + keysProperties['ad_mobs_interstitial_release'] + '\"'
        }
    }
    flavorDimensions "tier"
    productFlavors {
        free {
            dimension "tier"
            applicationId 'com.example.recipe_q'
        }
        paid {
            dimension "tier"
            applicationId 'com.example.recipe_q'
        }
    }
}

dependencies {
    def ad_service_version = "18.3.0"
    def android_layouts_version = "1.1.0"
    def constraints_version = "1.1.3"
    def firebase_version = "20.1.0"
    def lifecycle_version = "2.1.0"
    def material_version = "1.0.0"
    def picasso_version = "2.71828"
    def retrofit_version = "2.6.2"
    def room_version = "2.2.3"
    def junit_java_version = "4.12"
    def junit_android_version = "1.1.1"
    def espresso_version = "3.2.0"

    annotationProcessor "androidx.room:room-compiler:$room_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.android.material:material:$material_version"
    implementation "com.squareup.picasso:picasso:$picasso_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "androidx.appcompat:appcompat:$android_layouts_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraints_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.preference:preference:$android_layouts_version"
    implementation "androidx.recyclerview:recyclerview:$android_layouts_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_version"

    freeImplementation "com.google.firebase:firebase-ads:$ad_service_version"

    testImplementation "junit:junit:$junit_java_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_android_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
}
